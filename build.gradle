plugins {
    id 'java-library'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.github.hierynomus.license" version "0.15.0"
}

group = 'de.eosts'
version = '1.0.0-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

ext['pactVersion'] = '4.0.6'
ext['wireMockVersion'] = '2.25.1'

dependencies {
    api "com.github.tomakehurst:wiremock-jre8:${wireMockVersion}"
    api "au.com.dius:pact-jvm-provider:${pactVersion}"
    api "au.com.dius:pact-jvm-core-model:${pactVersion}"

    testImplementation "au.com.dius:pact-jvm-consumer:${pactVersion}"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.10"
    testImplementation 'junit:junit:4.12'
}

repositories {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

wrapper {
    gradleVersion = '6.2'
}

license {
    header = file('HEADER')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'EOS Technology Solutions GmbH'
}

//tasks.withType(Sign) {
//    onlyIf { isReleaseVersion }
//}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pact-stubs'
            from components.java
            pom {
                name = 'Pact Stubs'
                description = 'Generate Stubs from Pacts for WireMock and Messaging'
                url = 'https://github.com/EOS-TS/pact-stubs'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cmuelhau'
                        name = 'Christian Muelhaupt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}
